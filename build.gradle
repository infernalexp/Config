apply plugin: 'java'
apply plugin: 'maven-publish'

ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.electronwill.night-config', name: 'core', version: '3.6.4'
    implementation group: 'com.electronwill.night-config', name: 'toml', version: '3.6.4'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = config.GROUP
            artifactId = config.ARTIFACT
            from components.java

            pom {
                name = 'Config'
                url = 'https://infernalstudios.org/config-lib'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/infernalexp/Config.git'
                    url = 'https://github.com/infernalexp/Config'
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://maven.infernalstudios.org/snapshots' : 'https://maven.infernalstudios.org/releases' 
            
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_TOKEN')
            }
            
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

// Config parse function
def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return new ConfigSlurper().parse(prop)
    }
}
